#version 330 core
layout (location = 0) out vec4 FragColor;
layout (location = 1) out vec4 BrightColor;

in vec3 FragPos;
in vec3 normal;
in vec2 TexCoords;
in vec4 NearFragPosLightSpace;
in vec4 FarFragPosLightSpace;
in vec4 OutlineFragPosSpace;

in float depth;
in mat4 projMatrixInv;
in mat4 viewMatrixInv;

uniform sampler2D texture_diffuse1;
uniform sampler2D nearShadowMap;
uniform sampler2D farShadowMap;
uniform sampler2D outlineMap;
uniform sampler2D outlineMapNoLandscape;

uniform bool renderingLand;

uniform vec3 lightColor;
uniform vec3 shadowColor;
uniform vec3 fogColor;
uniform vec3 sun;
uniform float band;
uniform float gradient;
uniform float shift;
uniform float outlineSensitivity;
uniform float outlineTransparency;
uniform float minBias;
uniform float maxBias;
uniform float fogDepth;

vec3 WorldPos(float depthPos, vec2 TexCoord) {
    float z = depthPos * 2.0 - 1.0;

    vec4 clipSpacePosition = vec4(TexCoord * 2.0 - 1.0, z, 1.0);
    vec4 viewSpacePosition = projMatrixInv * clipSpacePosition;

    // Perspective division
    viewSpacePosition /= viewSpacePosition.w;

    vec4 worldSpacePosition = viewMatrixInv * viewSpacePosition;

    return worldSpacePosition.xyz;
}

float OutlineCalculation(vec4 outlineFragPosSpace) {
    vec3 projCoords = outlineFragPosSpace.xyz / outlineFragPosSpace.w;  // perform perspective divide
    projCoords = projCoords * 0.5 + 0.5;                                // transform to [0,1] range
    vec2 texelSize = 1.0 / textureSize(outlineMap, 0);
    int thickness = 2;
    float nearest = 0.0;
    vec2 nearestOffset = vec2(0.0);
    for(int x = -thickness; x <= thickness; ++x) {                                      // determine whether point should be part of an outline
        for(int y = -thickness; y <= thickness; ++y) {
            if (sqrt(pow(x, 2) + pow(y, 2)) > thickness) continue;
            float currentDepth = texture(outlineMap, projCoords.xy).r;
            float currentDepthNoLand = texture(outlineMapNoLandscape, projCoords.xy).r;

            vec2 offset = projCoords.xy + vec2(x, y) * texelSize;
            float offsetDepth = texture(outlineMap, offset).r;
            float offsetDepthNoLand = texture(outlineMapNoLandscape, offset).r;

            if (offsetDepth > currentDepth) continue;
            if (length(WorldPos(currentDepth, projCoords.xy) - WorldPos(offsetDepth, offset)) > outlineSensitivity &&
                (offsetDepth < nearest || nearest == 0.0)) {
                nearest = offsetDepth;
                nearestOffset = offset;
            }
            if (length(WorldPos(currentDepthNoLand, projCoords.xy) - WorldPos(offsetDepthNoLand, offset)) > outlineSensitivity &&
                (offsetDepthNoLand < nearest || nearest == 0.0)) {
                if (offsetDepth == offsetDepthNoLand) {
                    nearest = offsetDepth;
                    nearestOffset = offset;
                }
            }
        }    
    }
    return length(WorldPos(nearest, nearestOffset) - WorldPos(0.0, nearestOffset)) * fogDepth;
}

float ShadowCalculation(vec4 nearFragPosLightSpace, vec4 farFragPosLightSpace) {
    float shadow = 0.0;
    vec3 projCoords = nearFragPosLightSpace.xyz / nearFragPosLightSpace.w;  // perform perspective divide
    projCoords = projCoords * 0.5 + 0.5;                                    // transform to [0,1] range
    if (projCoords.x <= 1.0 && projCoords.x >= 0.0 &&                       // perform calculations for near shadow map
        projCoords.y <= 1.0 && projCoords.y >= 0.0 &&
        projCoords.z <= 1.0 && projCoords.z >= 0.0) {
        float closestDepth = texture(nearShadowMap, projCoords.xy).r;       // get closest depth value from light's perspective (using [0,1] range fragPosLight as coords)
        float currentDepth = projCoords.z;                                  // get depth of current fragment from light's perspective
        float bias = max(maxBias * (1.0 - dot(normal, normalize(sun))), minBias);   // determine "peter-panning" bias
        bias /= (100.0 - 0.1) / (7.5 - 1.0);

        vec2 texelSize = 1.0 / textureSize(nearShadowMap, 0);
        for(int x = -1; x <= 1; ++x) {                                      // check whether current frag pos is in shadow
            for(int y = -1; y <= 1; ++y) {
                float pcfDepth = texture(nearShadowMap, projCoords.xy + vec2(x, y) * texelSize).r; 
                shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;     
            }    
        }
        shadow /= 9.0;
    }
    else {                                                                  // repeat process for far shadow map
        projCoords = farFragPosLightSpace.xyz / farFragPosLightSpace.w;
        projCoords = projCoords * 0.5 + 0.5;
        float closestDepth = texture(farShadowMap, projCoords.xy).r; 
        float currentDepth = projCoords.z;
        float bias = max(0.06 * (1.0 - dot(normal, normalize(sun))), 0.012);
        bias /= (100.0 + 500.0) / (7.5 - 1.0);
        vec2 texelSize = 1.0 / textureSize(farShadowMap, 0);
        for(int x = -1; x <= 1; ++x) {
            for(int y = -1; y <= 1; ++y) {
                float pcfDepth = texture(farShadowMap, projCoords.xy + vec2(x, y) * texelSize).r; 
                shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;     
            }    
        }
        shadow /= 9.0;
    }

    return shadow;
}

vec3 LightCalculation(float shadow) {
    vec3 light;
    float nl = dot(normal, normalize(sun));
    vec3 middleColor = (lightColor + shadowColor) / 2.0;

    if (nl > shift + band + gradient) {
        light = (1.0 - shadow)*lightColor + (shadow)*middleColor;
    }

    else if (nl > shift + band - gradient) {
        float i = (nl - (shift + band - gradient)) / (2.0*gradient);
        if (renderingLand) {
            light = i*((1.0-shadow)*lightColor + (shadow)*middleColor) + (1-i)*((1.0 - shadow)*middleColor + (shadow)*shadowColor);
        }
        else {
            light = i*lightColor + (1-i)*middleColor;
            light = (1.0 - shadow)*light + (shadow)*middleColor;
        }
    }

    else if (nl > shift - band + gradient) {
        if (renderingLand) {
            light = (1.0 - shadow)*middleColor + (shadow)*shadowColor;
        }
        else {
            light = middleColor;
        }
    }

    else if (nl > shift - band - gradient) {
        float i = (nl - (shift - band - gradient)) / (2.0*gradient);
        light = i*middleColor + (1-i)*shadowColor;
        if (renderingLand) {
            light = (1.0 - shadow)*light + (shadow)*shadowColor;
        }
    }

    else
        light = shadowColor;

    return light;
}

void main(){
    vec4 outlineMultiplier = vec4(1.0, 1.0, 1.0, 1.0);

    float shadow = ShadowCalculation(NearFragPosLightSpace, FarFragPosLightSpace);      // Calculate if point is in shadow
    vec3 light = LightCalculation(shadow);                                              // Calculate light color
    //float outline = OutlineCalculation(OutlineFragPosSpace);                            // Calculate if point is in an outline
    //if (outline > 1.0) outline = 1.0;


    FragColor = vec4(texture(texture_diffuse1, TexCoords).xyz * light, texture(texture_diffuse1, TexCoords).a);
    FragColor = vec4((1.0-depth)*FragColor.xyz + (depth)*fogColor, FragColor.a);

    /*vec4 FragColorWithOutline;
    if (outline > 0.0) {
        FragColorWithOutline = vec4(0.0, 0.0, 0.0, 1.0);
        FragColorWithOutline = vec4((1.0-outline)*FragColorWithOutline.xyz + (outline)*fogColor, FragColor.a);
    }
    else FragColorWithOutline = FragColor;
    FragColor = vec4((1.0-outlineTransparency)*FragColorWithOutline + (outlineTransparency)*FragColor);*/

    if (length(FragColor.xyz) > length(vec3(0.8, 0.8, 0.8))) BrightColor = vec4(FragColor.xyz / 3.0, FragColor.a);
    else BrightColor = vec4(0.0, 0.0, 0.0, 1.0);
}