#version 330 core
layout (location = 0) out vec4 FragColor;
layout (location = 1) out vec4 BrightColor;

in vec3 FragPos;
in vec3 normal;
in vec2 TexCoords;
in vec4 NearFragPosLightSpace;
in vec4 FarFragPosLightSpace;
in vec4 OutlineFragPosSpace;

in float depth;
in mat4 projMatrixInv;
in mat4 viewMatrixInv;

uniform sampler2D texture_diffuse1;
uniform sampler2D nearShadowMap;
uniform sampler2D nearShadowMap2;
uniform sampler2D farShadowMap;
uniform sampler2D farShadowMap2;
uniform sampler2D outlineMap;
uniform sampler2D outlineMapNoLandscape;

uniform bool renderingLand;
uniform bool doFog;
uniform float skyBloom;

uniform vec3 lightColor;
uniform vec3 shadowColor;
uniform vec3 fogColor;
uniform vec3 sun;
uniform float band;
uniform float gradient;
uniform float shift;
uniform float outlineSensitivity;
uniform float outlineTransparency;
uniform float minBias;
uniform float maxBias;
uniform float fogDepth;

vec3 WorldPos(float depthPos, vec2 TexCoord) {
    float z = depthPos * 2.0 - 1.0;

    vec4 clipSpacePosition = vec4(TexCoord * 2.0 - 1.0, z, 1.0);
    vec4 viewSpacePosition = projMatrixInv * clipSpacePosition;

    // Perspective division
    viewSpacePosition /= viewSpacePosition.w;

    vec4 worldSpacePosition = viewMatrixInv * viewSpacePosition;

    return worldSpacePosition.xyz;
}

float ShadowCalculation(vec4 nearFragPosLightSpace, vec4 farFragPosLightSpace) {
    if (!doFog) return 0.0;
    bool inShadowOfLand = true;

    float shadow = 0.0;
    vec3 projCoords = nearFragPosLightSpace.xyz / nearFragPosLightSpace.w;  // perform perspective divide
    projCoords = projCoords * 0.5 + 0.5;                                    // transform to [0,1] range
    if (projCoords.x <= 1.0 && projCoords.x >= 0.0 &&                       // perform calculations for near shadow map
        projCoords.y <= 1.0 && projCoords.y >= 0.0 &&
        projCoords.z <= 1.0 && projCoords.z >= 0.0) {
     
        if (texture(nearShadowMap2, projCoords.xy).r < 1.0) inShadowOfLand = false;

        float currentDepth = projCoords.z;                                  // get depth of current fragment from light's perspective
        float bias = max(maxBias * (1.0 - dot(normal, normalize(sun))), minBias);   // determine "peter-panning" bias
        bias /= (100.0 - 0.1) / (7.5 - 1.0);

        vec2 texelSize = 1.0 / textureSize(nearShadowMap, 0);
        for(int x = -1; x <= 1; ++x) {                                      // check whether current frag pos is in shadow
            for(int y = -1; y <= 1; ++y) {
                float pcfDepth = texture(nearShadowMap, projCoords.xy + vec2(x, y) * texelSize).r; 
                shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;     
            }    
        }
        shadow /= 9.0;
    }
    else {                                                                  // repeat process for far shadow map
        projCoords = farFragPosLightSpace.xyz / farFragPosLightSpace.w;
        projCoords = projCoords * 0.5 + 0.5;

        //if (texture(farShadowMap, projCoords.xy).r == texture(farShadowMap2, projCoords.xy).r) inShadowOfLand = false;
        if (texture(farShadowMap2, projCoords.xy).r < 1.0) inShadowOfLand = false;

        float currentDepth = projCoords.z;
        float bias = max(0.06 * (1.0 - dot(normal, normalize(sun))), 0.012);
        bias /= (100.0 + 500.0) / (7.5 - 1.0);
        vec2 texelSize = 1.0 / textureSize(farShadowMap, 0);
        for(int x = -1; x <= 1; ++x) {
            for(int y = -1; y <= 1; ++y) {
                float pcfDepth = texture(farShadowMap, projCoords.xy + vec2(x, y) * texelSize).r; 
                shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;     
            }    
        }
        shadow /= 9.0;
    }
    if (inShadowOfLand) return -shadow;
    return shadow;
}

vec3 LightCalculation(float shadow) {
    if (!doFog) return vec3(1.0, 1.0, 1.0);
    bool complexShadows = renderingLand;
    if (shadow < 0.0) {
        shadow *= -1.0;
        complexShadows = false;
    }
    vec3 light;
    float nl = dot(normal, normalize(sun));
    vec3 middleColor = (lightColor + shadowColor) / 2.0;

    if (nl > shift + band + gradient) {
        light = (1.0 - shadow)*lightColor + (shadow)*middleColor;
    }

    else if (nl > shift + band - gradient) {
        float i = (nl - (shift + band - gradient)) / (2.0*gradient);
        if (complexShadows) {
            light = i*((1.0-shadow)*lightColor + (shadow)*middleColor) + (1-i)*((1.0 - shadow)*middleColor + (shadow)*shadowColor);
        }
        else {
            light = i*lightColor + (1-i)*middleColor;
            light = (1.0 - shadow)*light + (shadow)*middleColor;
        }
    }

    else if (nl > shift - band + gradient) {
        if (complexShadows) {
            light = (1.0 - shadow)*middleColor + (shadow)*shadowColor;
        }
        else {
            light = middleColor;
        }
    }

    else if (nl > shift - band - gradient) {
        float i = (nl - (shift - band - gradient)) / (2.0*gradient);
        light = i*middleColor + (1-i)*shadowColor;
        if (complexShadows) {
            light = (1.0 - shadow)*light + (shadow)*shadowColor;
        }
    }

    else
        light = shadowColor;

    return light;
}

void main(){
    float shadow = ShadowCalculation(NearFragPosLightSpace, FarFragPosLightSpace);      // Calculate if point is in shadow
    vec3 light = LightCalculation(shadow);                                              // Calculate light color
    
    FragColor = vec4(texture(texture_diffuse1, TexCoords).xyz * light, texture(texture_diffuse1, TexCoords).a);
    if (doFog) FragColor = vec4((1.0-depth)*FragColor.xyz + (depth)*fogColor, FragColor.a);

    if (length(FragColor.xyz) > length(vec3(0.8, 0.8, 0.8))) BrightColor = vec4(FragColor.xyz / 3.0, FragColor.a);
    else BrightColor = vec4(0.0, 0.0, 0.0, 1.0);

    if (!doFog) BrightColor = vec4(FragColor.xyz / skyBloom, FragColor.a);
}